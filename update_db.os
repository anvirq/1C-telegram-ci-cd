#Использовать v8rac
#Использовать v8runner

АдресСервера = "localhost";
ВерсияПлатформы = "8.3";

ИмяИнформационнойбазы = АргументыКоманднойСтроки[0];
Логин = АргументыКоманднойСтроки[1] ;
Пароль = ?(АргументыКоманднойСтроки.Количество() > 2,АргументыКоманднойСтроки[2], "");


УправлениеКластером = Новый УправлениеКластером;
УправлениеКластером.УстановитьКластер(АдресСервера);
УправлениеКластером.ИспользоватьВерсию(ВерсияПлатформы);

Попытка
	УправлениеКластером.Подключить();
Исключение
	Сообщить("Ошибка при попытке подключения к серверу администрирования, возможно не запущен ras");
	ЗавершитьРаботу(1);
КонецПопытки;

УправлениеКластером.УстановитьАвторизациюИнформационнойБазы(ИмяИнформационнойбазы, Логин, Пароль);
Сообщить("Блокируем начало сеансов и регламентных заданий");

Попытка
	УправлениеКластером.БлокировкаИнформационнойБазы(ИмяИнформационнойбазы,,"SecretKey"); 
Исключение
	Сообщить("Возникла ошибка при попытке блокировки:" + Символы.ПС + Символы.ПС + 
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	ЗавершитьРаботу(1);
КонецПопытки;

Сообщить("Отключаем все сеансы");
УправлениеКластером.ОтключитьСеансыИнформационнойБазы(ИмяИнформационнойбазы);

Попытка
	Конфигуратор = Новый УправлениеКонфигуратором();
	Конфигуратор.УстановитьКонтекст("/IBConnectionString""Srvr=" + АдресСервера + ";Ref='" + ИмяИнформационнойбазы + "'"" /UC SecretKey", Логин ,Пароль);

	Сообщить("Начинаем обновление конфигурации БД");
	Конфигуратор.ОбновитьКонфигурациюБазыДанных();
Исключение
	УправлениеКластером.СнятьБлокировкуИнформационнойБазы(ИмяИнформационнойбазы);
	Сообщить("Ошибка при обновлении:" + КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	ЗавершитьРаботу(1);
КонецПопытки;
Сообщить("База успешно обновлена, снимаем блокировку");
УправлениеКластером.СнятьБлокировкуИнформационнойБазы(ИмяИнформационнойбазы);
